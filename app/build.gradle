apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'


// use git to get the current branch.
static def gitBranch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}
// use git to get the current branch identifier.
static def gitBuildId() {
    def buildId = ""
    def proc = "git rev-parse  HEAD".execute()
    proc.in.eachLine { line -> buildId = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    buildId
}
// use git to get the current branch identifier.
static def gitCommitDate() {
    def commitDate = ""
    def proc = "utils/gitCommitDate.pl".execute()
    proc.in.eachLine { line -> commitDate = line }
    proc.err.eachLine { line -> commitDate line }
    proc.waitFor()
    commitDate
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.kana_tutor.notes"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 2
        versionName "2.0.0-${gitBranch()}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'long', 'BUILD_TIMESTAMP', String.format("0x%08xL", System.currentTimeMillis())
        buildConfigField "String", "BRANCH_NAME", "\"${gitBranch()}\""
        buildConfigField "String", "BUILD_ID", "\"${gitBuildId()}\""
        buildConfigField "String", "COMMIT_DATE", "\"${gitCommitDate()}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            applicationIdSuffix ".rel"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".dbg"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
